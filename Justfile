set shell := ["bash", "-euo", "pipefail", "-c"]
cargo_target_dir := env_var_or_default("CARGO_TARGET_DIR", "target")

default:
    @just --list

# Format the rust code
fmt:
    cargo fmt --all --check

# Check code
check:
    cargo check --workspace --all-targets --locked

# Run workspace tests
test:
    # Several tests own real subprocesses; serialize them so teardown cannot
    # race another fixture's process lifecycle in CI.
    cargo test --workspace --all-targets --locked -- --test-threads=1

# Run public-boundary Gherkin scenarios.
bdd:
    cargo test --package hologram-live --features bdd --test bdd --locked

# Compile and execute the locked NumPy + pandas example in a .holo archive.
python-holo-demo:
    ./scripts/check-python-holo-demo.sh

# Compile and execute the small standard-library Python example.
python-hello-demo:
    cargo build --release --locked --package hologram-live --bin hologram
    "{{cargo_target_dir}}/release/hologram" --json compile examples/python-hello/hologram.json --check >/dev/null
    "{{cargo_target_dir}}/release/hologram" --json run examples/python-hello --input-text Ada --output-format json

# Compile both Python Component examples and prove direct + resident execution
# plus isolation from the developer Python environment.
python-component-holo-demo:
    cargo test --release --locked --test python_component -- --ignored --nocapture

# Compile the locked Python Component in isolated tool caches and compare every
# canonical and physical archive identity. Emits one JSON document on stdout.
python-component-repro builds="2":
    ./scripts/check-python-component-reproducibility.sh --build-count "{{builds}}"

# Compile, verify, and retain the NumPy + pandas .holo artifact.
python-holo-package output="target/numpy-pandas.holo":
    ./scripts/check-python-holo-demo.sh --output "{{output}}"

# Build the NumPy/pandas rootfs without Docker's build cache and compare identities.
python-rootfs-repro builds="2":
    ./scripts/check-python-rootfs-reproducibility.sh --build-count "{{builds}}"

# Resolve, compile, and run through a disposable authenticated loopback registry.
python-private-registry:
    ./scripts/check-python-private-registry.sh

# Build a pinned kappa-registry and run provider conformance against it.
kappa-registry:
    ./scripts/check-kappa-registry.sh

# Keep production source files small enough to review and refactor.
file-size:
    ./scripts/check-file-size.sh

# The Kappa store pin is the documented one, and nothing forbidden is in the registry graph.
kappa-pin:
    ./scripts/check-kappa-pin.sh

# No layer in memory, and no Kappa type outside src/oci_store/.
oci-streaming:
    ./scripts/check-oci-streaming.sh

# The registry (cargo feature `oci`, off by default) keeps compiling and its tests keep passing.
oci-check:
    cargo check --locked --features oci --all-targets
    cargo test --locked --features oci --lib oci_store -- --test-threads=1
    cargo test --locked --features oci --test oci_store -- --test-threads=1
    cargo test --locked --features oci --lib -- modules::oci module::tests
    cargo test --locked --features oci --test oci_http

# Keep the standalone server dependency graph free of desktop code.
product-boundary:
    ./scripts/check-product-boundaries.sh

# Run clippy
clippy:
    cargo clippy --workspace --all-targets --locked -- -D warnings

# Build the standalone server binary.
server-build:
    cargo build --release --locked --package hologram-live --bin hologram

# Build the Tauri desktop application and its bundled server sidecar.
desktop-build:
    cd apps/desktop && npm ci && npm run build

# Default release build for the server.
build: server-build

# Verify code
verify: fmt file-size product-boundary kappa-pin oci-streaming check oci-check test clippy bdd build
    ./scripts/smoke.sh "{{cargo_target_dir}}/release/hologram"

# Run project
run *args:
    cargo run --locked --package hologram-live --bin hologram -- {{args}}

# Recompile and restart the foreground daemon when Rust/server inputs change.
dev:
    @command -v cargo-watch >/dev/null || { echo "error: just dev requires cargo-watch (cargo install cargo-watch --locked)" >&2; exit 1; }
    cargo watch --clear --watch src --watch proto --watch build.rs --watch Cargo.toml --watch Cargo.lock --exec 'run --locked --package hologram-live --bin hologram -- serve'

# Build the docs
# Model Hub site (apps/model-hub/web): catalog snapshot, brand token lint, static build → apps/model-hub/web/dist.
# Set BASE=/path/ when served under a path; GitHub Pages publishes it at /<repository>/model-hub/.
model-hub-site:
    npm ci --prefix apps/model-hub/web
    test -f apps/model-hub/web/data/models.json || node apps/model-hub/web/scripts/data.mjs --limit 500
    node apps/model-hub/web/scripts/lint-tokens.mjs
    node apps/model-hub/web/build.mjs

# Refresh the catalog snapshot (trending Hugging Face models joined with the Hologram address index).
model-hub-data:
    node apps/model-hub/web/scripts/data.mjs --limit 500

docs:
    HOLOGRAM_CONFIG="{{justfile_directory()}}/target/docs-config/live.toml" cargo run --locked --package hologram-live --bin hologram -- --json openapi --output apps/docs/public/openapi.json
    cd apps/docs && npm ci && npm run build

# Validate, tag, and push the current documentation version to GitHub Pages.
docs-release version="":
    ./scripts/release-docs.sh "{{version}}"

# Serve the Astro docs with hot reload.
docs-dev:
    cd apps/docs && npm install && npm run dev -- --host 127.0.0.1 --port 54321

# Work with the Tauri desktop app. Defaults to `dev`; `just tauri build` creates a bundle.
tauri action="dev":
    cd apps/desktop && npm install && npm run "{{action}}"
